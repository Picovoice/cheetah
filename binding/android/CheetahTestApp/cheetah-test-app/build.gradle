apply plugin: 'com.android.application'

Properties properties = new Properties()
if (rootProject.file("local.properties").exists()) {
    properties.load(rootProject.file("local.properties").newDataInputStream())
    if (project.hasProperty("pvTestingAccessKey")) {
        properties.put("pvTestingAccessKey", project.getProperty("pvTestingAccessKey"))
    }
    if (project.hasProperty("numTestIterations")) {
        properties.put("numTestIterations", project.getProperty("numTestIterations"))
    }
    if (project.hasProperty("initPerformanceThresholdSec")) {
        properties.put("initPerformanceThresholdSec", project.getProperty("initPerformanceThresholdSec"))
    }
    if (project.hasProperty("procPerformanceThresholdSec")) {
        properties.put("procPerformanceThresholdSec", project.getProperty("procPerformanceThresholdSec"))
    }

    if (project.hasProperty("storePassword")) {
        properties.put("storePassword", project.getProperty("storePassword"))
    }
    if (project.hasProperty("storeFile")) {
        properties.put("storeFile", project.getProperty("storeFile"))
    }
    if (project.hasProperty("keyAlias")) {
        properties.put("keyAlias", project.getProperty("keyAlias"))
    }
    if (project.hasProperty("keyPassword")) {
        properties.put("keyPassword", project.getProperty("keyPassword"))
    }
}

android {
    compileSdkVersion defaultTargetSdkVersion

    defaultConfig {
        applicationId "ai.picovoice.cheetah.testapp"
        minSdkVersion 21
        targetSdkVersion defaultTargetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue 'string', 'pvTestingAccessKey', properties.getProperty("pvTestingAccessKey", "")
        resValue 'string', 'numTestIterations', properties.getProperty("numTestIterations", "")
        resValue 'string', 'initPerformanceThresholdSec', properties.getProperty("initPerformanceThresholdSec", "")
        resValue 'string', 'procPerformanceThresholdSec', properties.getProperty("procPerformanceThresholdSec", "")
    }

    signingConfigs {
        release {
            storePassword properties.getProperty("storePassword")
            storeFile file(properties.getProperty("storeFile", ".dummy.jks"))
            keyAlias properties.getProperty("keyAlias")
            keyPassword properties.getProperty("keyPassword")
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.release
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    if (System.getProperty("testBuildType", "debug") == "integ") {
        testBuildType("release")
    }

    def testDataFile = file('../../../../resources/.test/test_data.json')
    def parsedJson = new groovy.json.JsonSlurper().parseText(testDataFile.text)
    def languages = []
    parsedJson.tests.parameters.each { a ->
        languages.add(a.language)
    }

    flavorDimensions "language"
    productFlavors {
        en {
            getIsDefault().set(true)
        }

        languages.each { language ->
            "$language" {
                applicationIdSuffix ".$language"

            }
        }

        all { flavor ->
            delete fileTree("$projectDir/src/main/assets") {
                exclude '**/.gitkeep'
            }
            String suffix = (flavor.name != "en") ? "_${flavor.name}" : ""
            task("${flavor.name}CopyParams", type: Copy) {
                from("$projectDir/../../../../lib/common/")
                include("cheetah_params${suffix}.pv")
                into("$projectDir/src/main/assets/models")
            }
            task("${flavor.name}CopyAudio", type: Copy) {
                description = "Copy ${flavor.name} audio resources"
                from("$projectDir/../../../../resources/audio_samples/")
                include("test${suffix}.wav")
                into("$projectDir/src/main/assets/audio_samples")
            }
        }
    }
    sourceSets {
        androidTest {
            java {
                if (System.getProperty("testBuildType", "debug") == "perf") {
                    exclude "**/StandardTests.java"
                    exclude "**/IntegrationTest.java"
                    exclude "**/LanguageTests.java"
                } else if (System.getProperty("testBuildType", "debug") == "integ") {
                    exclude "**/StandardTests.java"
                    exclude "**/LanguageTests.java"
                    exclude "**/PerformanceTest.java"
                } else {
                    exclude "**/IntegrationTest.java"
                    exclude "**/PerformanceTest.java"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        abortOnError false
    }

    namespace 'ai.picovoice.cheetah.testapp'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.code.gson:gson:2.10'
    implementation 'ai.picovoice:cheetah-android:2.1.1'

    // Espresso UI Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation('androidx.test.espresso:espresso-intents:3.5.1')
}

afterEvaluate {
    android.productFlavors.all {
        flavor ->
            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyParams"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyParams"
            tasks."generate${flavor.name.capitalize()}ReleaseLintVitalReportModel".dependsOn "${flavor.name}CopyParams"
            tasks."lintVitalAnalyze${flavor.name.capitalize()}Release".dependsOn "${flavor.name}CopyParams"
            tasks."merge${flavor.name.capitalize()}DebugAssets".dependsOn "${flavor.name}CopyAudio"
            tasks."merge${flavor.name.capitalize()}ReleaseAssets".dependsOn "${flavor.name}CopyAudio"
            tasks."generate${flavor.name.capitalize()}ReleaseLintVitalReportModel".dependsOn "${flavor.name}CopyAudio"
            tasks."lintVitalAnalyze${flavor.name.capitalize()}Release".dependsOn "${flavor.name}CopyAudio"
    }
}
